~ village_entrance
if randomizer()
	Your Consciousness: You've finally made it! Now it is time to sell your produce.
	Your Consciousness: You can stay in a village for as long as you have gold, or else you'll get kicked out of your room at the inn!
	Your Consciousness: Make sure you keep an eye on it.
	=> village_day
else
	do currentLocation.emit("plague")
	Your Consciousness: A stench hangs in the air of this village... It appears they have the plague.
	Your Consciousness: The black death is a terrible sickness that leaves awful boils on someone's body. 
	Your Consciousness: You can barely bear to think about it.
	Your Consciousness: Should you leave?
	- No, they could use some extra produce and help.
		if luck >= 2
			Your Consciousness: You hand out your wares for free, helping to brighten a few people's days.
			Your Consciousness: You are very careful when you hand them items as to not catch the plague.
			Your Consciousness: You have given away all your wares, made 0 gold, and are not sick.
			do leftVillage.emit("escape_sickness")
		elif luck == 1
			Your Consciousness: You attempt to sell your items. You take gold from patrons.
			Your Consciousness: One of the coins was contaiminated and before long, you catch the plague.
			do leftVillage.emit("dead")
		else
			Your Consciousness: You enter the village and immedieately trip over a body.
			Your Consciousness: The very ground is cursed and you feel yourself become sick.
			do leftVillage.emit("dead")
			=> END
	- Yes I am getting out of here.
		do leftVillage.emit("plague_leave")
			
=> END

~ village_day
set hasExplored = false
do currentLocation.emit("normal")
Your Consciousness: Now... Where should you set up shop?
- In the village center
	Your Consciousness: This looks like a nice spot... Right at the center of everything!
	Your Consciousness: Look, here comes your first interested person.
	% =>< little_girl
	% =>< merchant_guy
		#change the background of the scene depending on where you choose
		#determine how well sales are depending on your luck
- At the village entrance
	Your Consciousness: You set up everything right by the village entrance. Many weary travelers pass you by.
	Your Consciousness: You begin to wonder if anyone cares to buy anything.
	if randomizer()
		Your Consciousness: Wait, here comes someone now!
		=>< merchant_guy
		=> end_of_day
	Your Consciousness: Aw, it looked like everyone has passed you by...
	Your Consciousness: They didn't take well to someone shilling right by the entrance.
- Tucked into a corner at the market
	do currentLocation.emit("market")
	Your Consciousness: You wonder if anyone will be able to see you here.
	Your Consciousness: The other stalls look so flashy and large compared to your own.
	if randomizer()
		Your Consciousness: Oh, here comes someone now.
		=>< angry_chef
		=> end_of_day
	Your Consciousness: Oh no, it looks like everyone passed you up...
	Your Consciousness: Maybe you could try for a better spot next time.
- In a good spot at the market [if SignalBus.luck >= 3]
	do currentLocation.emit("market")
	Your Consciousness: You manage to find the perfect spot at the market. 
	Your Consciousness: You've sold all your produce in one day!
	set soldEverything = true
- I want to explore the village instead
	Your Consciousness: You decide to explore the village instead of selling your produce.
	=> explore_town
=> end_of_day

~ end_of_day
do currentLocation.emit("inn")
Nothing else notable happened that day and you return back to the inn for the night.
if gold <= 0
	Your Consciousness: What? I'm fresh out of gold?
	Your Consciousness: The inkeep won't let me stay like this!
	- Ask the inkeep if you can stay one more night.
		if luck >= 3
			The inkeep is kind enough to let you stay one more night.
			You sleep in bed riddled with anxiety and quickly leave the village the next morning.
			do leftVillage.emit("no_gold")
			=> END
		else
			Inkeep: This isn't a charity! Scram!
			You are kicked out onto the streets and forced to journey home.
			do leftVillage.emit("shame")
			=> END
	- Leave the village to avoid shame
		Your Consciousness: You quickly leave the village in the night with no gold.
		do leftVillage.emit("no_gold")
	=> END!

The inkeep takes one of your gold for lodging.
set SignalBus.gold -= 1
do dayEnded.emit()
if hasExplored == false
	Your Consciousness: Whew, that was a long day of selling. This bed feels pretty comfortable, hopefully there aren't any critterous tennants...
else
	Your Consciousness: Exploring the village was quite the experience.

if soldEverything
	Your Consciousness: You've sold all your produce, so there's no reason for you to stay unless you want to explore the village.
	Your Consciousness: Should you explore tomorrow?
	- Yes
		Your Consciousness: You tuck into bed with plans of exploring the village tomorrow.
		set hasExplored = true
		It is already the next day in the village.
		=> explore_town
	- No
		Your Consciousness: You do feel rather tired from all the excitement of leaving your small farm.
		Your Consciousness: You tuck into bed and smile with plans of returning home tomorrow.
		do leftVillage.emit("sold_everything")
	=> END
	
Your Consciousness: Should I leave the village tomorrow morning or stay one more day?
- I'm leaving
	You tuck into bed with plans to leave the village tomorrow.
	Early next morning, you pack everything up and leave.
	do SignalBus.leftVillage.emit("normal_leave")
	=> END
- I want to stay
	You tuck into bed with dreams of what to do tomorrow.
	It's the next day already.
	=> village_day
=> END!

~ merchant_guy
Traveling Merchant: Ah thee again. The same peasant from the roads.
- Good Day!
- Could thou prithee cease dubbing me a peasant?
	Traveling Merchant: Wherefore wouldst I cease dubbing thee what thee art? How follish of thee.
	set luck -= 1
	do playSound(negSound)
	Traveling Merchant: I wanted to buy m're of thy produce, but t appears thee has developed an attitude.
	Traveling Merchant: Goodbye.
	Your Consciousness: Yikes...
	=> END
- Ay it's me.
Traveling Merchant: Good morrow! Prithee allow me to buy m're of thy produce.
- Ay, of course!
	The merchant buys some of your produce for 3 gold.
	set goldGrowth += 3
	Traveling Merchant: It is truly so lovely doing business with you. Give ye safe journeys.
- No...
	Traveling Merchant: Nay? I'm s'rry? How can thou dare to reject mine own business at which hour thee obviously need the coin?
	Traveling Merchant: I'm afraid thee has't gone mad. Farewell.
	Your Consciousness: Bruh...

=> END

~ angry_chef
Petulant Chef: I need 10 parsnips. Now.
- Sure...
	Petulant Chef: Don't dilly dally. Hurry up!
	Petulant Chef: I've got dozens of mouths to feed at the inn and I'm fresh out of parsnips.
	Your Consciousness: You rush to get the chef his parsnips.
	You gain 10 gold.
	set goldGrowth += 10
- I don't know if I have that many. [if daysInVillage >= 1]
	Petulant Chef: What do you mean? What kind of merchant are you?
	- You should have got here sooner, then.
		set luck -= 1
		do playSound(negSound)
		Petulant Chef: Plague upon your crops. I'll buy them elsewhere.
		
	- I'm s'rry, but I already sold a lot of them.
		He huffs loudly and walks off. Oops.
=> END

~ little_girl
Little Girl: Good morrow. What are you doing?
- Selling produce
	Little Girl: Oh. Do you have anything good?
	You describe what you're selling, including wheat and parsnips. The little girl seems to light up.
	Little Girl: I think my mom could use some of those. Here!
	set goldGrowth += 1
	She hands you 1 gold coin in exchange for 3 parsnips.
	- Thank you so much!
		Little Girl: You're welcome! I'll tell everyone else about you too. Bye!
		Your Consciousness: Success! You feel better about having traveled to the market.
		
- I don't know
	Little Girl: Well I don't either. That's why I asked you.
	Little Girl: You're silly! You can have this.
	She hands you 1 gold coin.
	- Please take it back.
		Little Girl: No it's ok! Silly people deserve it.
		Your Consciousness: You gain 1 gold.
		set goldGrowth += 1
		set SignalBus.luck += 1
		Your Consciousness: Aw that was sweet of her. It really brightened your day!
		
	- Oh thanks.
		You take her gold coin and she smiles before skipping off.
		Your Consciousness: You gain 1 gold.
		do playSound(negSound)
		set goldGrowth += 1
		Your Consciousness: Did you just scam a little girl? Maybe you should have tried harder to tell her to keep it.
		set SignalBus.luck -= 1
			
	- Where's your parents?
		Little Girl: I'm big enough to get around on my own!
		Little Girl: I had even brought some gold with me today. But I don't want your stinky veggies anyways!
		Your Consciousness: Oh no... You feel kind of bad about offending her, but she's already stomped off.
		=> END
=> END

~ town_crier
You encounter the town crier shouting out the latest news.
Your Consciousness: This guy is so loud, gosh.
% Town Crier: [shake rate=20.0 level=5 connected=1]HEAR YE, HEAR YE![/shake] Francis Drake completes voyage around the world!
% Town Crier: [shake rate=20.0 level=5 connected=1]HEAR YE, HEAR YE![/shake] Bailiffs crack down on draughts gambling!
% Town Crier: [shake rate=20.0 level=5 connected=1]HEAR YE, HEAR YE![/shake] Rumors of the Queen's secret lover afloat! Will our country gain a king?  
- Continue walking
	You walk away but you can still hear him in the distance.
- Stay to listen more
	do playSound(posSound)
	set luck += 1
	You learn more about the current events of the time, feeling well informed.
=> END

~ the_inkeeper
Inkeep: Hail and well met! Back already?
- I'm just looking around.
	Inkeep: Alright. Try not to make a ruckus.
	There is a group of workers playing draughts at one of the tables.
	- Watch the game
		It's obvious that the group knows each other well. Various jokes and insults are thrown across the board as they play.
		You learn some new playing strategies and a few inside jokes.
	- Ask to join
		if luck < 1
			The group stares at you awkwardly before declining.
			You sit down at another table and occasionally glance back at them.
		else
			do playSound(posSound)
			set luck += 1
			The group stares at you before one of the workers heartily agrees.
			You sit down and play, joining the banter and enjoying the game.
			You learn a bit more about current events and the atmosphere of the village.
			
- Could I get something to eat here?
	Inkeep: Yeah, just talk to the chef over there.
	Across the room is another counter behind which a man rushes wildly through a kitchen.
	You approach hesitantly.
	- Hello?
		Petulant Chef: ...
		Your Consciousness: Is he going to say anything? Maybe I should try again.
		- *Louder* Hello??
			Petulant Chef: I heard you the first time. I don't have time for small talk and I wouldn't want to have it with you anyways.
			Petulant Chef: He turns back to the several boiling pots.
			- I just want some food...
				Petulant Chef: He sneers at you over his shoulder.
				=> food_ask
			- Maybe I should leave him alone
				
		- May I have some food?
			=> food_ask
	- May I have some food?
		=> food_ask
=> END

~ food_ask
Petulant Chef: He tosses a bowl of hot gruel onto the counter without saying a word.
Your Consciousness: I wonder if I have to pay for this...
- Leave a coin
	do playSound(posSound)
	set gold -= 1
	set luck += 1
	You leave 1 gold on the counter before taking your food.
	Petulant Chef: He hastily swipes the coin into his pocket and returns to cooking.
- Take it without paying
	do playSound(negSound)
	set luck -= 1
	You take the gruel without paying and eat at a nearby table.
	Despite the chef continuing to work rapidly, you can feel his eyes boring into you.
=> END

~ explore_town
set hasExplored = true
Your Consciousness: Where do you want to go?
- The Inn
	do currentLocation.emit("inn")
	=>< the_inkeeper
- The Market
	do currentLocation.emit("market")
	You walk to the market in the village center. It is busy and bustling.
	Various stalls and carts litter the square while others lay their wares out directly on the ground before them.
	=>< town_crier 
#- The Village Green 
#	This option doesn't do anything yet. Sorry.
	
=> end_of_day